plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

// Legge versione direttamente dal pubspec.yaml
def pubspecFile = rootProject.file('../pubspec.yaml')
def pubspecText = pubspecFile.text
def versionMatch = (pubspecText =~ /version:\s*(\S+)\+(\d+)/)
def flutterVersionName = versionMatch ? versionMatch[0][1] : '1.0.0'
def flutterVersionCode = versionMatch ? versionMatch[0][2].toInteger() : 1

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.withReader('UTF-8') { reader ->
        keystoreProperties.load(reader)
    }
}
android {
    namespace "com.stereo98.dabplus"
    compileSdkVersion 36
    ndkVersion flutter.ndkVersion
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        applicationId "com.stereo98.dabplus"
        multiDexEnabled true
        minSdkVersion 25
        targetSdkVersion 35
        versionCode flutterVersionCode
        versionName flutterVersionName
    }
    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}
flutter {
    source '../..'
}
